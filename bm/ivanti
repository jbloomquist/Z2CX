@ECHO OFF
TITLE Ivanti Fix Script
SETLOCAL ENABLEDELAYEDEXPANSION
SET mint=[38;5;121m
SET re=[0m
:: Script Purpose Statement
ECHO This script is designed to delete specific files to trigger a full repair security scan.
ECHO %mint%Please be patient with the script, even if it looks like nothing is happening.
ECHO %re%This script will prompt for Admin Elevation after you continue:
ENDLOCAL
PAUSE

:: Admin Prompt
NET SESSION >NUL 2>&1 || (ECHO [!] Admin required & PowerShell -Command "Start-Process '%~f0' -Verb RunAs" & EXIT /B)

:: Initialize Environment
SET "IVANTI_ROOT=C:\Program Files (x86)\Ivanti\EPM Agent"
SET "CBAROOT=%IVANTI_ROOT%\Shared Files\cbaroot"
SET "BASE_ENGINE=%IVANTI_ROOT%\Base Engine"

:: Delete Everything But 1 cert
IF EXIST "%CBAROOT%" CD /D "%CBAROOT%"
IF EXIST "broker" (ECHO [+] Deleting broker\ &&DEL /Q /F "broker\*.*" >NUL 2>&1 &FOR /D %%D IN ("broker\*") DO RMDIR /S /Q "%%D") ELSE (ECHO [-] broker\ missing)
IF EXIST "certs" (FOR %%F IN ("certs\*") DO (ECHO %%~nxF|FINDSTR /I "^B3.*\.0$" >NUL || (ECHO     Deleting %%~nxF & DEL /Q /F "%%F"))) ELSE (ECHO [-] certs\ missing )
IF EXIST "%BASE_ENGINE%" (IF EXIST "%BASE_ENGINE%\BrokerConfig.exe" (ECHO [x] Executing BrokerConfig Routines... &&GOTO BC_ROUTINE) ELSE (ECHO [-] BrokerConfig.exe missing; Exiting &&GOTO IV_QUIT)) ELSE (ECHO [-] Base Engine missing; Exiting &&GOTO IV_QUIT)

:: BrokerConfig.exe Routine
:BC_ROUTINE
ECHO Requesting Ivanti Broker Certificates...
"C:\Program Files (x86)\Ivanti\EPM Agent\Base Engine\BrokerConfig.exe" /r && ECHO     Certificates Retrieved &ECHO( 

:: Security Scan (vulscan.exe) Routine
:SS_ROUTINE
ECHO [x] Running Security Scan...
"C:\Program Files (x86)\Ivanti\EPM Agent\Patch Management\vulscan.exe" /showui=true && ECHO     Security Scan Complete &ECHO( 

:: Inventory Scan (ldiscn32.exe) Routine
:IS_ROUTINE
ECHO [x] Running Inventory Scan - Full Scan // This might take a while, be patient.
"C:\Program Files (x86)\Ivanti\EPM Agent\Inventory\ldiscn32.exe" /F /V && ECHO Inventory Scan Complete &ECHO( 
GOTO IV_QUIT

:IV_QUIT
ECHO Ivanti Fix Script Complete
ECHO This script will now close &&TIMEOUT /T 5
EXIT /B
